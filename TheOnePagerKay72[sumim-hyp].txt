--- DISCLAIMER ---
Unofficial, lightly amended reconstruction of Alan Kay's 1972 "The One Pager" by sumim -- includes 
typo fixes, control-flow repairs, and reasoned adjustments (e.g., non-local return for "^").
For the unmodified original, see TheOnePagerKay72.txt or "The Early History of Smalltalk" (Kay, 1993),
Appendix II, Figure 11A.3 ("The One Pager"), available at, e.g.:
https://tinlizzie.org/VPRIPapers/hc_smalltalk_history.pdf
------------------

e (the environment) will be bound to the current Messenger object
result holds the result of a send, usually to be *applied* to next part of message

```
eval: if null(e●MSG●(e●PC))    then goto handlnil;
      if escape(e●MSG●(e●PC))  then goto escapes;
      if atom(e●MSG●(e●PC))    then 'result ← lookupvalue(e, e●MSG●(e●PC)); goto apply;
      if notlist(e●MSG●(e●PC)) then 'result ← e●MSG●(e●PC); goto apply;

evlist: 'e ← Table(CLASS,  MESSENGER,
                    SNDR,   e,
                    HOME,   e●HOME,
                    GLOBAL, e●GLOBAL,
                    SELF,   e●SELF,
                    PC,     1,
                    MSG,    e●MSG●(e●PC),
                    RTN,    APPLY);
        goto eval;

apply: if null(e●MSG●(e●PC)) then goto handlnil;
       e●'PC ← e●PC + 1;
       if e●PC > length(e●MSG) then goto dispatchrtn;
       if e●MSG●(e●PC) = '.  then e●'PC ← e●PC + 1; 'result ← NIL; goto eval;
       if e●MSG●(e●PC) = '»  then if result = FALSE
                                      then e●'PC ← e●PC + 2; goto eval;
                                      else e●'PC ← e●PC + 1;
                                           'e ← Table(CLASS,  MESSENGER,
                                                       SNDR,   e,
                                                       HOME,   e●HOME,
                                                       GLOBAL, e●GLOBAL,
                                                       SELF,   e●SELF,
                                                       PC,     1,
                                                       MSG,    e●MSG●(e●PC),
                                                       RTN,    FROMTRUE);
                                           goto eval;

       'e ← Table(CLASS,  MESSENGER,
                   SNDR,   e,
                   HOME,   NIL,
                   GLOBAL, e●GLOBAL,
                   SELF,   result,
                   PC,     1,
                   MSG,    result●CLASS●CODE,
                   RTN,    APPLY);
       e●'HOME ← e;
       goto eval;

fromTRUE: 'e ← e●SNDR●SNDR; goto apply;

fromEVLB: e●SNDR●SNDR●HOME●HOME●SNDR●'PC ← e●PC;
          putvalue(e●SNDR●SNDR, e●SNDR●p, result);
          'e ← e●SNDR●SNDR; goto apply;

fromTHRU: 'e ← e●SNDR;
          if e●'RTN = FROMEVLB 
　　　　　　　then e●'PC ← e●PC - 1;
　　　　　　　　　　goto dispatchrtn; 
　　　　　　　else goto eval;

handlnil: if e = e●HOME then 'result ← e●SELF; e●'RTN ← FROMTHRU;
          goto dispatchrtn;

dispatchrtn: select e●RTN
                    case APPLY:    'e ← e●SNDR; goto apply;
                    case FROMTRUE: goto fromTRUE;
                    case FROMEVLB: goto fromEVLB;
                    case FROMTHRU: goto fromTHRU;

escapes: e●'PC ← e●PC + 1;
         select e●MSG●(e●PC)
                 etc...
```

```
to ¤ (metacodefor(
        if e●SNDR●HOME●SNDR●MSG●(e●SNDR●HOME●SNDR●PC) = e●SNDR●MSG●(e●SNDR●PC)
                then bump(e●SNDR●HOME●SNDR, PC); 'result ← TRUE;
                else 'result ← FALSE;
        'e ← e●SNDR; goto apply; ))

to : p (⦂ p . metacodefor(
               'e ← Table(CLASS,  MESSENGER,
                           SNDR,   e,
                           HOME,   e●SNDR●HOME●HOME●SNDR●HOME,
                           GLOBAL, e●SNDR●HOME●HOME●SNDR●GLOBAL,
                           SELF,   e●SNDR●HOME●HOME●SNDR●SELF,
                           PC,     e●SNDR●HOME●HOME●SNDR●PC,
                           MSG,    e●SNDR●HOME●HOME●SNDR●MSG,
                           RTN,    FROMEVLB);
               goto eval; ))

to ⦂ p v (metacodefor(
           e●'v ← e●SNDR●HOME●SNDR●MSG●(e●SNDR●HOME●SNDR●PC);
           bump(e●SNDR●HOME●SNDR, PC);
           if null(e●'p ← e●SNDR●MSG●(e●SNDR●PC))
               then 'result ← e●v;
               else 'result ← e●v; putvalue(e●SNDR, e●p, result);
           'e ← e●SNDR; goto apply; ))


to ^ b (: b. metacodefor('result ← e●b; 'e ← e●HOME●SNDR; goto apply;))
```
